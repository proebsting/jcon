#SRC: APP

#  profsum -- read and summarize profiling data from "java -prof"
#
#  usage:  profsum [-t] [file]	-- typically: profsum java.prof
#
#	-t	sort on total time instead of time in method
#
#  3-jul-97/gmt
#  5-jun-98/gmt



record method(name, ncalls, tself, tcalled, tlocal)

global ptab



procedure main(args)
   local f, line, p, l, tlocal, ttotal, pctlocal, pcttotal, sorttotal

   if args[1] == "-t" then
      sorttotal := get(args)

   if *args > 0 then
      f := open(args[1]) | stop(&progname, ": can't open ", args[1])
   else
      f := &input

   while line := read(f) do line ? {
      if ="count" then
          next
      if not any(&digits) then
          break
      accumulate()
      }

   l := []
   ttotal := 0
   every p := !ptab do {
      p.tlocal := p.tself - p.tcalled
      if p.tlocal > 0 then
         ttotal +:= p.tlocal
      put(l, p)
   }

   if \sorttotal then
      l := sortf(l, 3)
   else
      l := sortf(l, 5)

   write("  calls  totaltime   inmethod  method")

   while p := pull(l) do {
      if p.tlocal > 0 then
	 pctlocal := (100 * p.tlocal + ttotal / 2) / ttotal
      else
	 pctlocal := (100 * p.tlocal - ttotal / 2) / ttotal
      pcttotal := ((100 * p.tself) + (ttotal / 2)) / ttotal
      write(
	 pad(p.ncalls, 7),
	 pad(p.tself, 7), " ",
         pad(pcttotal, 2), "%",
	 pad(p.tlocal, (if pcttotal = 100 then 6 else 7)), " ",
         pad(pctlocal, 2), "%",
	 "  ",
	 p.name);
      }

end



procedure lookup(s)
   local p
   initial ptab := table()

   if p := \ptab[s] then
      return p
   else
      return ptab[s] := method(s, 0, 0, 0)
end



procedure accumulate()
   local count, caller, callee, time, p, q

   count := integer(tab(many(&digits)))
   tab(many(' '))
   callee := tab(upto(' '))
   tab(many(' '))
   caller := tab(upto(' '))
   if caller ||:= =" caller" then
      caller ||:= tab(upto(' '))
   tab(many(' '))
   time := integer(tab(many(&digits)))

   caller := fixname(caller)
   callee := fixname(callee)

   p := lookup(callee)
   p.tself +:= time
   p.ncalls +:= count

   q := lookup(caller)
   q.tcalled +:= time

   return
end



procedure fixname(s)
   s ? {
      # strip "meaningless" prefix
      ="rts/" | (="java/" & tab(upto('/') + 1)) | (="p_l$" & tab(upto('$')))
      return tab(0)
   }
end



#  pad(s, n) -- pad string to length n, never truncating

procedure pad(s, n)
   if *s < n then
      return right(s, n)
   else
      return s
end
